/*
  Copyright 2020 Neopsis, All Rights Reserved.
 */

import groovy.io.FileVisitResult
import groovy.io.FileType

pluginManagement {
    apply from: 'environment.gradle'

    resolutionStrategy {
        eachPlugin {
            if (requested.id.namespace == 'com.tridium') {
                useModule('com.tridium.tools:niagara-plugins:4.1.5')
            }
        }
    }
    repositories {
        maven {
            url "${gradle.ext.niagara_home}/etc/m2/repository"
        }
        gradlePluginPortal()
    }
}

def discoveredProjects = [:] as Map<String, File>

ext {
    niagaraRoots = ['envasDemo-wb']
    excludeDirs = ['.hg', 'build', 'out', 'src', 'srcTest']
}

def environment = file('environment.gradle')
if (environment.exists()) {
    apply from: environment
}

niagaraRoots.collect({ file(it) }).findAll({ it.exists() }).each { File projectRoot ->
    projectRoot.traverse(
            type: FileType.DIRECTORIES,
            preRoot: true,
            preDir: { File projectDir ->
                def projectName = projectDir.name
                if (excludeDirs.contains(projectName)) {
                    return FileVisitResult.SKIP_SUBTREE
                }

                File buildScript = new File(projectDir, "${projectName}.gradle")
                if (buildScript.exists()) {
                    discoveredProjects[projectName] = projectDir
                    include projectName
                    return FileVisitResult.SKIP_SUBTREE
                }
            }
    )
}

rootProject.name = 'envasDemo'
rootProject.children.each { project ->
    project.projectDir = discoveredProjects[project.name]
    project.buildFileName = "${project.name}.gradle"
    assert project.projectDir.isDirectory()
    assert project.buildFile.isFile()
}
